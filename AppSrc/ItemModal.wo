// E:\DataFlex Projects\DiscoveringDataFlexWebDemo\AppSrc\Item.wo
// Item
//

Use cWebView.pkg
Use cWebPanel.pkg
Use cWebForm.pkg
Use cWebEdit.pkg

Use clocationDataDictionary.dd
Use citemDataDictionary.dd
Use cDefaultLocationDataDictionary.dd
Use cWebButton.pkg
Use cWebFileUploadButton.pkg
Use cWebImage.pkg
Use cWebResourceManager.pkg

Object oItemModal is a cWebModalDialog
    Set piColumnCount to 12
    Set psCaption to "Item Modal"
    Set piWidth to 700

    Object oDefaultLocation_DD is a cDefaultLocationDataDictionary
    End_Object

    Object olocation_DD is a clocationDataDictionary
    End_Object 

    Object oitem_DD is a citemDataDictionary
        Set DDO_Server to olocation_DD
        Set DDO_Server to oDefaultLocation_DD

    End_Object 

    Set Main_DD to oitem_DD
    Set Server  to oitem_DD

    { WebProperty=Server }
    Property String psFilePath
    
    Object oItemResourceManager is a cWebResourceManager
    End_Object

    Object oWebMainPanel is a cWebPanel
        Set piColumnCount to 12

        Object oWebImage1 is a cWebImage
            Set piColumnSpan to 9
            Set pbFillHeight to True
            Set psCSSClass to "item_image"
        End_Object

        Object oWebFileUploadButton1 is a cWebFileUploadButton
            Set piColumnSpan to 9
            Set psCaption to "Upload File(s)"
        
            // Called for each file upload to generate the local path where the file is stored.
            Function OnFileUpload String sFileName Integer iBytes String sMime Returns String
                String sPath
                Boolean bFileExists
                Integer iItemID
                
                Get Field_Current_Value of oitem_DD Field Item.ID to iItemID
                // Determine local path based on workspace setting
                Get psDataPath of (phoWorkspace(ghoApplication)) to sPath
                File_Exist (sPath + "\Uploads") bFileExists
                If (not(bFileExists)) Make_Directory (sPath + "\Uploads")
                Move (sPath + "\Uploads") to sPath
                
                File_Exist (sPath + "\" + String(iItemID)) bFileExists
                If (not(bFileExists)) Make_Directory (sPath + "\" + String(iItemID))
                Move (sPath + "\" + String(iItemID)) to sPath
                
                Send RegisterDownloadFolder of oItemResourceManager sPath
                
                EraseFile (sPath + "\*.*")
                
                Move (sPath + "\" + sFileName) to sPath
                
                WebSet psFilePath of oItemModal to sPath
                
                Function_Return sPath
            End_Function
            
            Procedure OnUploadFinished 
                String sPath
                
                WebGet psFilePath of oItemModal to sPath
                
                //WebSet psURL of oWebImage1 to sPath
                Send UpdateLocalImage of oWebImage1 sPath
            End_Procedure
        End_Object

        Object oitemName is a cWebForm
            Entry_Item Item.Name
            Set piColumnSpan to 9
            Set piColumnIndex to 0
            Set peLabelPosition to lpTop
            Set psLabel to "Name"
        End_Object 

        Object oitemDescription is a cWebEdit
            Entry_Item Item.Description
            Set piColumnSpan to 9
            Set piColumnIndex to 0
            Set peLabelPosition to lpTop
            Set psLabel to "Description"
            Set piHeight to 100
        End_Object 

        Object oitemCategory is a cWebCombo
            Entry_Item Item.Category
            Set piColumnSpan to 4
            Set piColumnIndex to 0
            Set peLabelPosition to lpTop
            Set psLabel to "Category"
        End_Object 

        Object olocationName is a cWebForm
            Entry_Item location.Name
            Set piColumnSpan to 5
            Set piColumnIndex to 4
            Set peLabelPosition to lpTop
            Set psLabel to "Location"
        End_Object 

        Object oitemExpirationDate is a cWebDateForm
            Entry_Item Item.ExpirationDate
            Set piColumnSpan to 2
            Set piColumnIndex to 0
            Set peLabelPosition to lpTop
            Set psLabel to "ExpirationDate"
        End_Object 

        Object oWebButtonSave is a cWebButton
            Set piColumnSpan to 3
            Set psCaption to "Save"
            Set piColumnIndex to 6
            Set pbShowLabel to True
            Set peLabelPosition to lpTop
        
            Procedure OnClick
                Send Request_Save of oitem_DD
                Send Ok
            End_Procedure
        End_Object
    End_Object 
    
    // used to initialize the dialog
    Procedure Popup Handle hoReturnObject Integer iItemID
        Forward Send Popup hoReturnObject Self
        
        Send Request_Clear of oitem_DD
        Move iItemID to Item.ID
        Send Request_Find of oitem_DD EQ Item.File_Number 1

// or
        
//        Send Clear of oitem_DD
//        Move iItemID to Item.ID
//        Send Find of oitem_DD EQ 1

        String sPath sFileName sValue
        Boolean bFileExists
        
        If (iItemID > 0) Begin
            Get psDataPath of (phoWorkspace(ghoApplication)) to sPath
            File_Exist (sPath + "\Uploads") bFileExists
            If (not(bFileExists)) Make_Directory (sPath + "\Uploads")
            Move (sPath + "\Uploads") to sPath
            
            File_Exist (sPath + "\" + String(iItemID)) bFileExists
            If (not(bFileExists)) Make_Directory (sPath + "\" + String(iItemID))
            Move (sPath + "\" + String(iItemID)) to sPath
            
            Send RegisterDownloadFolder of ghoWebResourceManager sPath
            
            Move False to bFileExists
            Direct_Input ("dir: " + sPath)
            Repeat
                Readln sFileName
                If (sFileName <> "" and sFileName <> "[.]" and sFileName <> "[..]") Begin
                  Move (sPath + "\" + sFileName) to sValue
                    Move True to bFileExists
                    //Get DownloadURL of ghoWebResourceManager (sPath + "\" + sFileName) to sValue
                End
            Until (SeqEof)
            Close_Input ("dir: " + sPath)
            
            If (not(bFileExists)) Move ('https://source.unsplash.com/random/800x800/?img=' + String(iItemID)) to sValue
            Send UpdateLocalImage of oWebImage1 (sValue)
        End
        
    End_Procedure
    
    Procedure OnBeforeShow
        If (WebAppUser.Rights = 0) Begin
            WebSet pbRender of oWebFileUploadButton1 to False
            WebSet pbEnabled of oitemName to False
            WebSet pbEnabled of oitemDescription to False
            WebSet pbEnabled of oitemCategory to False
            WebSet pbEnabled of olocationName to False
            WebSet pbEnabled of oitemExpirationDate to False
            WebSet pbRender of oWebButtonSave to False
        End
    End_Procedure
    
End_Object 

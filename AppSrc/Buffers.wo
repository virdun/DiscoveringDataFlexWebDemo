Use cWebView.pkg
Use cWebPanel.pkg
Use cWebForm.pkg 
Use clocationDataDictionary.dd
Use cDefaultLocationDataDictionary.dd
Use citemDataDictionary.dd
Use cWebButton.pkg
Use cWebHtmlBox.pkg

Open Item

Object oBuffers is a cWebView
    Set piWidth to 700
    Set psCaption to "Buffers"

    Object oDefaultLocation_DD is a cDefaultLocationDataDictionary
    End_Object

    Object olocation_DD is a clocationDataDictionary
    End_Object

    Object oitem_DD is a citemDataDictionary
        Set DDO_Server to oDefaultLocation_DD
        Set DDO_Server to olocation_DD
    End_Object

    Set Main_DD to oitem_DD
    Set Server to oitem_DD
    
    Object oWebMainPanel is a cWebPanel
        Set piColumnCount to 12

        Object oDataEntryID is a cWebForm
            Entry_Item Item.ID
            Set piColumnSpan to 5
            Set psLabel to "Item ID"
            Set pbReadOnly to True
        End_Object

//        Object oRefindRecordsBtn is a cWebButton
//            Set piColumnSpan to 4
//            Set psCaption to "Send Refind_Records of hoDD"
//            Set piColumnIndex to 5
//        
//            Procedure OnClick
//                Send Refind_Records of oitem_DD
//                Send UpdateBuffers
//                Send UpdateTable
//            End_Procedure
//        End_Object

        Object oDataEntry is a cWebForm
            Entry_Item Item.Name
            Set piColumnSpan to 5
            Set psLabel to "Data Entry"
        End_Object

        Object oMoveToTable is a cWebButton
            Set piColumnSpan to 4
            Set psCaption to "Send Request_Save of hoDD"
            Set piColumnIndex to 5
        
            Procedure OnClick
                Boolean bErr
                String sValue
                
                //WebGet psValue of oDataEntry to sValue
                //Set Field_Changed_Value of oitem_DD (RefTable(Item.Name)) to sValue
                
                Get Validate_Save of oitem_DD to bErr
                If (not(bErr)) Send Request_Save of oitem_DD
                
//                Send UpdateFields
            End_Procedure
        End_Object

        Object oSeedBox is a cWebHtmlBox
            Set piColumnSpan to 3
            Set psHtml to "<h1>Input</h1>"
            Set piColumnIndex to 3
        End_Object
        
        Object oUpdateBuffers is a cWebButton
            Set piColumnSpan to 3
            Set psCaption to "Read buffers"
            Set piColumnIndex to 8
            Set psBackgroundColor to "#005187"
        
            Procedure OnClick
                Send UpdateBuffers
            End_Procedure
        End_Object

        Object oLocalBuffer is a cWebForm
            Set piColumnSpan to 5
            Set psLabel to "Local DD Buffer"
        End_Object

        Object oSetDdBtn is a cWebButton
            Set psCaption to "Set >"
            Set piColumnIndex to 5
        
            Procedure OnClick
                String sValue

                WebGet psValue of oLocalBuffer to sValue
                Set Field_Changed_Value of oitem_DD (RefTable(Item.Name)) to sValue

                Send UpdateBuffers
                Send UpdateTable
            End_Procedure
        End_Object

        Object oLocalBufferRead is a cWebForm
            Set piColumnSpan to 5
            Set psLabel to "Local DD Buffer"
            Set piColumnIndex to 6
            Set pbReadOnly to True
            Set pbEnabled to False
        End_Object
        
        Object oUpdateDDBuffers is a cWebButton
            Set psCaption to "Read"
            Set piColumnIndex to 11
            Set psBackgroundColor to "#005187"
        
            Procedure OnClick
                Send UpdateDDBuffers
            End_Procedure
        End_Object
        
        Object oGlobalBuffer is a cWebForm
            Set piColumnSpan to 5
            Set psLabel to "Global Buffer"
        End_Object

        Object oSetGbBtn is a cWebButton
            Set psCaption to "Set >"
            Set piColumnIndex to 5
        
            Procedure OnClick
                String sValue

                WebGet psValue of oGlobalBuffer to sValue
                Move sValue to Item.Name
                WebSet psValue of oGlobalBufferRead to Item.Name

                Send UpdateGBBuffers
            End_Procedure
        End_Object

        Object oGlobalBufferRead is a cWebForm
            Set piColumnSpan to 5
            Set psLabel to "Global Buffer"
            Set piColumnIndex to 6
            Set pbReadOnly to True
            Set pbEnabled to False
        End_Object
        
        Object oUpdateGBBuffers is a cWebButton
            Set psCaption to "Read"
            Set piColumnIndex to 11
            Set psBackgroundColor to "#005187"
        
            Procedure OnClick
                Send UpdateGBBuffers
            End_Procedure
        End_Object
        
        Object oSaveTable is a cWebButton
            Set psCaption to "Saverecord"
            Set piColumnIndex to 8
            Set piColumnSpan to 3
        
            Procedure OnClick
                String sValue
                
                WebGet psValue of oGlobalBufferRead to sValue
                Move sValue to Item.Name
                SaveRecord Item
                
                Send UpdateGBBuffers
                Send UpdateTable
            End_Procedure
        End_Object


        Object oTable is a cWebForm
            Set piColumnSpan to 5
            Set psLabel to "Table"
        End_Object

        Object oSetDbBtn is a cWebButton
            Set psCaption to "Set >"
            Set piColumnIndex to 5
        
            Procedure OnClick
                String sValue sTmp
                
                Move Item.Name to sTmp
                WebGet psValue of oTable to sValue
                Move sValue to Item.Name
                SaveRecord Item
                Move sTmp to Item.Name
                
                Send UpdateBuffers
                Send UpdateTable
            End_Procedure
        End_Object
        Object oTableReadId is a cWebForm
            Set piColumnSpan to 2
            Set psLabel to "Table"
            Set piColumnIndex to 6
            Set pbReadOnly to True
            Set pbEnabled to False
            Set piLabelOffset to 50
        End_Object

        Object oTableRead is a cWebForm
            Set piColumnSpan to 3
            Set psLabel to "Table"
            Set piColumnIndex to 8
            Set pbReadOnly to True
            Set pbEnabled to False
            Set pbShowLabel to False
        End_Object

        Object oUpdateTable is a cWebButton
            Set psCaption to "Read"
            Set piColumnIndex to 11
            Set psBackgroundColor to "#005187"
        
            Procedure OnClick
                Send UpdateTable
            End_Procedure
        End_Object
        
        
        
        
        
        Procedure UpdateBuffers
            String sValue

            Get Field_Current_Value of oitem_DD (RefTable(Item.Name)) to sValue
            WebSet psValue of oLocalBufferRead to sValue
            
            WebSet psValue of oGlobalBufferRead to Item.Name
        End_Procedure

        Procedure UpdateDDBuffers
            String sValue
            Get Field_Current_Value of oitem_DD (RefTable(Item.Name)) to sValue
            WebSet psValue of oLocalBufferRead to sValue
        End_Procedure

        Procedure UpdateGBBuffers
            WebSet psValue of oGlobalBufferRead to Item.Name
        End_Procedure
        
        Procedure UpdateTable
            String sValue

            If (Item.ID > 0) Begin
                Find EQ Item by Index.1
                WebSet psValue of oTableReadId to Item.Id
                WebSet psValue of oTableRead to Item.Name
            End
            Else Begin
                WebSet psValue of oTableReadId to "0"
                WebSet psValue of oTableRead to ""
            End
        End_Procedure

        Object oFindDbBtn is a cWebButton
            Set psCaption to "Find"
            Set piColumnSpan to 2
            Set piColumnIndex to 1
        
            Procedure OnClick
                String sValue
                
                WebGet psValue of oTable to sValue
                Clear Item
                Move sValue to Item.Name
                Find GE Item by Index.2
                
                Send UpdateBuffers
                //Send UpdateGBBuffers
                Send UpdateTable
            End_Procedure
        End_Object

        Object oReqFindDbBtn is a cWebButton
            Set psCaption to "Request_Find"
            Set piColumnIndex to 3
            Set piColumnSpan to 3
        
            Procedure OnClick
                String sValue
                
                WebGet psValue of oTable to sValue
                Send Clear of oitem_DD
                Move sValue to Item.Name
                Send Request_Find of oitem_DD GE Item.File_Number 2
                
                //Send UpdateBuffers
                //Send UpdateGBBuffers
                Send UpdateTable
            End_Procedure
        End_Object
        
        Object oReqFindSetBtn is a cWebButton
            Set psCaption to "Find + Set File_Field"
            Set piColumnIndex to 6
            Set piColumnSpan to 3
        
            Procedure OnClick
                String sValue
                
                WebGet psValue of oTable to sValue
                Clear Item
                Move sValue to Item.Name
                Find GE Item by Index.2
                
                Set Field_Current_Value of oitem_DD (RefTable(Item.ID)) to Item.ID
                Set Field_Current_Value of oitem_DD (RefTable(Item.Name)) to Item.Name
                
                Send UpdateBuffers
                Send UpdateTable
            End_Procedure
        End_Object
        
    End_Object 

    Procedure OnBeforeShow
        Forward Send OnBeforeShow
        
        WebSet pbRender of oFindToolBar to True
        WebSet pbRender of oFileToolBar to True
    End_Procedure
    
End_Object

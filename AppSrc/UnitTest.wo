Use cWebView.pkg
Use cWebPanel.pkg
Use cWebForm.pkg 
Use cWebGrid.pkg
Use cWebColumn.pkg
Use cWebColumnImage.pkg
Use cWebHtmlBox.pkg
Use cWebButton.pkg
Use cWebLabel.pkg

//Struct tWebCell
//    String sValue
//    String sTooltip
//    String[] aOptions
//End_Struct

//Struct tWebRow
//    String sRowID
//    String sCssClassName
//    tWebCell[] aCells
//End_Struct


Object oUnitTest is a cWebView
    Set piWidth to 700
    Set psCaption to "UnitTest"
    
    { WebProperty = Server }
    Property String psRowID

    Object oWebMainPanel is a cWebPanel
        Set piColumnCount to 12

        Object oRunTestsBtn is a cWebButton
            Set piColumnSpan to 2
            Set psCaption to "Run tests"
        
            Procedure OnClick
                WebSet psRowID of oUnitTest to "0"
                Send RunTests
            End_Procedure
        End_Object

        Object oErrorCount is a cWebLabel
            Set psCaption to "Errors found: 0"
            Set piColumnIndex to 9
            Set piColumnSpan to 3
            Set peAlign to alignRight
        End_Object

        Object oTestList is a cWebList
            Set pbFillHeight to False
            Set pbDataAware to False
            Set pbServerOnRowClick to True
            Set piHeight to 500
            
            Object oIconCol is a cWebColumnImage
                Set psCaption to "Ok"
//                Set psImageUrl to "Images/Order.png"
                Set piWidth to 30
                Set pbDynamic to True
                Set pbFixedWidth to True
            End_Object
            Object oTitleCol is a cWebColumn
                Set psCaption to "Title"
                Set piWidth to 50
            End_Object
            Object oTypeCol is a cWebColumn
                Set psCaption to "Type"
                Set piWidth to 0
            End_Object
            Object oInformationCol is a cWebColumn
                Set psCaption to "Information"
                Set piWidth to 0
            End_Object
            
            Procedure OnProcessDataSet tWebRow[] aData Integer eOperation Integer iSelectedRowIndex
                If (eOperation = 1) Send CountErrors aData
            End_Procedure

            Procedure CountErrors tWebRow[] aData
                Integer iCnt iSize iTotal
                Move 0 to iTotal
                Move 0 to iCnt
                Move (SizeOfArray(aData)) to iSize
                
                While (iCnt < iSize)
                    If (aData[iCnt].aCells[0].aOptions[0] = "Images/red_light.png") Begin
                        Increment iTotal
                    End
                    Increment iCnt
                Loop
                
                WebSet psCaption of oErrorCount to ("Errors found: " + (String(iTotal)))
            End_Procedure
            
            Procedure AppendRow tWebRow tTheRow
                //Integer iCount
                //WebGet piRowCount of oTestList to iCount
                //Move iCount to tTheRow.sRowId
                
                Send DataSetAppendRow of oTestList tTheRow 
            End_Procedure
            
            Procedure OnRowClick String sRowID
                String sInformation
                
                WebGet psValue of oInformationCol to sInformation
                
                WebSet psHtml of oInformationBox to sInformation
            End_Procedure
        End_Object

        Object oInformationBox is a cWebHtmlBox
            Set piColumnSpan to 0
            Set psHtml to "Here is where the information goes"
        End_Object
    End_Object
    
    Procedure AddRowToGrid Integer iStatus String sLabel String sType;
                            String sExpected String sResult
        tWebRow tTheRow
        String sIconPath sRowID
        Integer iRowID
        Move "Images/no_light.png" to sIconPath
        If (iStatus = 0) Move "Images/red_light.png" to sIconPath
        If (iStatus = 1) Move "Images/green_light.png" to sIconPath
        
        WebGet psRowID of oUnitTest to sRowID
        Move (Integer(sRowID)) to iRowID
        Increment iRowID
        WebSet psRowID of oUnitTest to (String(iRowID))
        
        Move (String(iRowID)) to tTheRow.sRowID
        Move sIconPath to tTheRow.aCells[0].aOptions[0]
        Move sLabel to tTheRow.aCells[1].sValue
        Move sType to tTheRow.aCells[2].sValue
        Move "" to tTheRow.aCells[3].sValue
        If (iStatus = 0) Move ("Expected: " + sExpected + "<br>Found: " + sResult) to tTheRow.aCells[3].sValue

        Send AppendRow of oTestList tTheRow
    End_Procedure
    
    Procedure TestGroup String sLabel
        Send AddRowToGrid -1 sLabel "group" "" ""
    End_Procedure
    
    Procedure Assert String sLabel String sExpected String sResult
        Integer iStatus
        Move 1 to iStatus
        If (sExpected <> sResult) Move 0 to iStatus
        
        Send AddRowToGrid iStatus sLabel "group" sExpected sResult
    End_Procedure
    
    Procedure RunTests
        String sResult
        Send TestGroup "Remove surounding appostrophs from string"

        Move (RemoveSuroundingApposFromString('"Test string"')) to sResult
        Send Assert "Check string" "Test string" sResult

        Move (RemoveSuroundingApposFromString('"Other string"')) to sResult
        Send Assert "Check other string" "Other string" sResult
        
        Move (RemoveSuroundingApposFromString('Regular string')) to sResult
        Send Assert "Check none appo string" "Regular string" sResult
        
        Send ProcessDataSet of oTestList 1 // Count errors
    End_Procedure

End_Object
